group 'de.dehn'
version '1.0'

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.5

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile group: 'org.springframework', name: 'spring-core', version: '4.2.4.RELEASE'
    compile group: 'org.springframework', name: 'spring-jdbc', version: '4.2.4.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-config', version: '4.0.3.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-web', version: '4.0.3.RELEASE'

    compile group: 'org.springframework.boot', name: 'spring-boot', version: '1.3.2.RELEASE'

    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.10'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DEHN+SÃ–HNE Spring Boot Drupal authentication'
                packaging 'jar'
                description 'Support for authentication with Spring Security against Drupal'
                url 'https://github.com/dehn-und-soehne/spring-boot-drupal-authentication'

                scm {
                    url 'https://github.com/dehn-und-soehne/spring-boot-drupal-authentication.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'stephangrundner'
                        name 'Stephan Grundner'
                        email 'stephangrundner@gmail.com'
                    }
                }
            }
        }
    }
}
